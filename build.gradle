buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
	}
}

plugins {
	id "maven"
	id "com.github.johnrengelman.shadow" version "1.2.3"
	id "com.matthewprenger.cursegradle" version "1.0.6"
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "kotlin"
apply from: "https://github.com/shadowfacts/maven/raw/master/maven.gradle"

version = minecraft_version + "-" + mod_version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
	version = minecraft_version + "-" + forge_version
	runDir = "run"

	mappings = mcp_mappings

	replaceIn "ShadowMC.java"
	replace '@VERSION@', mod_version
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", minecraft_version

	from (sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':minecraft_version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

repositories {
	jcenter()
	maven {
		name "shadowfacts"
		url "http://maven.shadowfacts.net/"
	}
}

dependencies {
	compile group: "org.projectlombok", name: "lombok", version: lombok_version
	compile group: "net.shadowfacts", name: "ShadowLib", version: shadowlib_version
	compile group: "net.shadowfacts", name: "ShadowConfig", version: shadowconfig_version
	compile group: "net.shadowfacts", name: "Mirror", version: mirror_version
	compile group: "com.udojava", name: "EvalEx", version: evalex_version
	compile group: "net.shadowfacts", name: "Forgelin", version: forgelin_version
}

shadowJar {
	classifier = ""
	dependencies {
		include(dependency("net.shadowfacts:ShadowLib:${shadowlib_version}"))
		include(dependency("net.shadowfacts:ShadowConfig:${shadowconfig_version}"))
		include(dependency("net.shadowfacts:Mirror:${mirror_version}"))
		include(dependency("com.udojava:EvalEx:${evalex_version}"))
	}
	exclude "test/*"
}

tasks.build.dependsOn sourceJar, shadowJar
tasks.publish.dependsOn shadowJar

artifacts {
	archives shadowJar
	archives sourceJar
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = "226780"

		addGameVersion "1.12"
		addGameVersion "Java 8"

		mainArtifact(shadowJar) {
			releaseType = "release"
		}
	}
}

reobf { shadowJar { mappingType = 'SEARGE' } }
tasks.reobfShadowJar.mustRunAfter shadowJar
afterEvaluate {
	tasks.curseforge226780.dependsOn reobfShadowJar
}
